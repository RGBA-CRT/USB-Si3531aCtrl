'-----------------------------------------------------------------------------
'  イベント プロシージャ
'-----------------------------------------------------------------------------
' このファイルには、ウィンドウ [MainWnd] に関するイベントをコーディングします。
' ウィンドウ ハンドル: hMainWnd

' TODO: この位置にグローバルな変数、構造体、定数、関数を定義します。


'-----------------------------------------------------------------------------
' ウィンドウメッセージを処理するためのコールバック関数

Function MainWndProc(hWnd As HWND, dwMsg As DWord, wParam As WPARAM, lParam As LPARAM) As DWord
	' TODO: この位置にウィンドウメッセージを処理するためのコードを記述します。

	'デバイス接続イベント
	if dwMsg=WM_DEVICECHANGE Then	ListupDevices()


	' イベントプロシージャの呼び出しを行います。
	MainWndProc=EventCall_MainWnd(hWnd,dwMsg,wParam,lParam)
End Function

Function MWnd(DlgID AS Long) AS HANDLE
	MWnd=GetDlgItem(hMainWnd,DlgID)
End Function

Function GetTrackBarValue(hWnd As HANDLE) AS Long
	GetTrackBarValue=SendMessage(hWnd,TBM_GETPOS,0,0)
End Function

Sub SetTrackBarValue(hWnd AS HANDLE,value AS DWord)
	SendMessage(hWnd,TBM_SETPOS,TRUE,value)
End Sub
'-----------------------------------------------------------------------------
' ここから下は、イベントプロシージャを記述するための領域になります。

Sub MainWnd_Destroy()
	FT232H_Si5351a_DestroyObjects()
	PostQuitMessage(0)
End Sub

Const ND_MAX = 1048575
Const VCODIV_MAX=1800
Const VCODIV_MIN=6
Sub MainWnd_Create(ByRef CreateStruct As CREATESTRUCT)
	SendMessage(MWnd(TrackBar1),TBM_SETRANGE,TRUE,MAKELONG(15,90))
	SendMessage(MWnd(TrackBar2),TBM_SETRANGEMIN	,TRUE,0)
	SendMessage(MWnd(TrackBar2),TBM_SETRANGEMAX	,TRUE,ND_MAX)
	SendMessage(MWnd(TrackBar3),TBM_SETRANGEMIN	,TRUE,1)
	SendMessage(MWnd(TrackBar3),TBM_SETRANGEMAX	,TRUE,ND_MAX)

	
	SendMessage(MWnd(TrackBar4),TBM_SETRANGE,TRUE,MAKELONG(VCODIV_MIN,VCODIV_MAX))
	SendMessage(MWnd(TrackBar5),TBM_SETRANGEMIN	,TRUE,0)
	SendMessage(MWnd(TrackBar5),TBM_SETRANGEMAX	,TRUE,ND_MAX)
	SendMessage(MWnd(TrackBar6),TBM_SETRANGEMIN	,TRUE,1)
	SendMessage(MWnd(TrackBar6),TBM_SETRANGEMAX	,TRUE,ND_MAX)
	SendMessage(MWnd(TrackBar7),TBM_SETRANGE,TRUE,MAKELONG(0,7))

	ApplyEditBox()

	ListupDevices()
End Sub

Sub ListupDevices()
	Dim devCount AS Long',str[1024] AS Byte

	SendMessage(MWnd(ComboBox1),CB_RESETCONTENT,0,0)

	FT_GetNumDevices(devCount,NULL,FT_LIST_BY_NUMBER_ONLY)
	if devCount=0 Then SendMessage(MWnd(ComboBox1),CB_ADDSTRING,0,"FTDI Device not found." AS Long):Goto *LD_END

	Dim i AS Long
	Dim serial[64] As Byte,desc[64] As Byte
	For i = 0 To devCount-1
		if FT_ListDevices(i,serial,FT_LIST_BY_INDEX or FT_OPEN_BY_SERIAL_NUMBER)<>FT_OK Then 
			SendMessage(MWnd(ComboBox1),CB_ADDSTRING,0,sprintfStr("[%02d] open err",i))
			Continue
		End If
		FT_ListDevices(i,desc  ,FT_LIST_BY_INDEX or FT_OPEN_BY_DESCRIPTION)

		SendMessage(MWnd(ComboBox1),CB_ADDSTRING,0, _
				sprintfStr(ex"[%02d]%s - %s\n",i,serial As DWord,desc AS DWord))
	Next i

*LD_END
	SendMessage(MWnd(ComboBox1),CB_SETCURSEL,0,0)

End Sub
Sub MainWnd_HScroll(nScrollCode As Long, nPos As Integer, hwndScrollBar As HWND)
	UpdateEditBox()
	DispValues()

End Sub

'トラックバーがへんこうされたとき
Sub UpdateEditBox()
	'vco
	SetWindowText(MWnd(EditBox1),Str$(GetTrackBarValue(MWnd(TrackBar1))))
	SetWindowText(MWnd(EditBox3),Str$(GetTrackBarValue(MWnd(TrackBar2))))
	SetWindowText(MWnd(EditBox4),Str$(GetTrackBarValue(MWnd(TrackBar3))))
	'fout
	SetWindowText(MWnd(EditBox5),Str$(GetTrackBarValue(MWnd(TrackBar4))))
	SetWindowText(MWnd(EditBox7),Str$(GetTrackBarValue(MWnd(TrackBar5))))
	SetWindowText(MWnd(EditBox8),Str$(GetTrackBarValue(MWnd(TrackBar6))))
	SetWindowText(MWnd(EditBox9),Str$(GetTrackBarValue(MWnd(TrackBar7))))
	SetWindowText(MWnd(Static17),"="+Str$(1<<GetTrackBarValue(MWnd(TrackBar7))))
End Sub

Sub UpdateTrackBar()	
	Dim conf As Si5351A_CONFIG
	GetClkgenConf(VarPtr(conf))
	With conf
		'vco
		SetTrackBarValue(MWnd(TrackBar1), .vcoA)
		SetTrackBarValue(MWnd(TrackBar2), .vcoB)
		SetTrackBarValue(MWnd(TrackBar3), .vcoC)
		'fout
		SetTrackBarValue(MWnd(TrackBar4), .divA)
		SetTrackBarValue(MWnd(TrackBar5), .divB)
		SetTrackBarValue(MWnd(TrackBar6), .divC)
		SetTrackBarValue(MWnd(TrackBar7), .R_DIV)
	EndWith
End Sub

Function CalcVCO() AS Double
	Dim conf As Si5351A_CONFIG
	GetClkgenConf(VarPtr(conf))

	With conf
		CalcVCO=.crystal * (.vcoA + .vcoB/.vcoC)
	End With
End Function

Function CalcFdiv() AS Double
	Dim conf As Si5351A_CONFIG
	GetClkgenConf(VarPtr(conf))

	With conf
		CalcFdiv=(.divA + (.divB/.divC)) * (1<<.R_DIV)
	End With
End Function

Function CalcFout() AS Double
	CalcFout=CalcVCO() / CalcFdiv()
End Function

Sub GetClkgenConf(conf As *Si5351A_CONFIG)
	conf->crystal= 	GetDlgItemInt(hMainWnd,EditBox2,0,FALSE)
	conf->vcoA	= 	GetDlgItemInt(hMainWnd,EditBox1,0,FALSE)
	conf->vcoB	= 	GetDlgItemInt(hMainWnd,EditBox3,0,FALSE)
	conf->vcoC	= 	GetDlgItemInt(hMainWnd,EditBox4,0,FALSE)
	conf->divA	= 	GetDlgItemInt(hMainWnd,EditBox5,0,FALSE)
	conf->divB	= 	GetDlgItemInt(hMainWnd,EditBox7,0,FALSE)
	conf->divC	= 	GetDlgItemInt(hMainWnd,EditBox8,0,FALSE)
	conf->div4	= 	0
	conf->R_DIV	= 	GetDlgItemInt(hMainWnd,EditBox9,0,FALSE) AS Byte
End Sub

Sub SetClkgenConf(conf As *Si5351A_CONFIG)
	'vco
	SetWindowText(MWnd(EditBox1), Str$(conf->vcoA))
	SetWindowText(MWnd(EditBox3), Str$(conf->vcoB))
	SetWindowText(MWnd(EditBox4), Str$(conf->vcoC))
	'fout
	SetWindowText(MWnd(EditBox5), Str$(conf->divA))
	SetWindowText(MWnd(EditBox7), Str$(conf->divB))
	SetWindowText(MWnd(EditBox8), Str$(conf->divC))
	SetWindowText(MWnd(EditBox9), Str$(conf->R_DIV))
	SetWindowText(MWnd(Static17),"="+Str$(1<<conf->R_DIV))
	UpdateTrackBar()
	UpdateEditBox()
	DispValues()
End Sub

Sub DispValues()
	Dim vco AS Double
	vco=CalcVCO()
	SetWindowText(MWnd(Static7),doubleToFormatStr("vco=%.01f[MHz]",vco))

	Dim Mes aS BytePtr
	if vco<600 or vco>900 Then
		Mes="VCO frequency in the range of 600 to 900 MHz."
	Else
		Mes="VCO OK"
	End If
	SetWindowText(MWnd(Static13),Mes)

	Dim fout AS Double,fdiv as Double,sim AS String
	fout=CalcFout()
	if fout > 1 Then
		sim="[MHz]"
	Else
		fout=fout*1000
		sim="[KHz]"
	End If
	
	SetWindowText(MWnd(Static12),doubleToFormatStr("fout=%5.03f",fout)+sim)


	fdiv=CalcFdiv()
	if fdiv<6 or fdiv>1800 Then
		Mes="diverter NG [6 < (a + (b/c) * RDiv) < 1800] "
	Else
		Mes="diverter OK"
	End If
	SetWindowText(MWnd(Static8),Mes)

End Sub

Sub ApplyEditBox()
	UpdateTrackBar()
	UpdateEditBox()	'もし範囲外なら戻される
	DispValues()
End Sub

Sub MainWnd_EditBox1_KillFocus()	: ApplyEditBox() : End Sub
Sub MainWnd_EditBox3_KillFocus()	: ApplyEditBox() : End Sub
Sub MainWnd_EditBox4_KillFocus()	: ApplyEditBox() : End Sub
Sub MainWnd_EditBox5_KillFocus()	: ApplyEditBox() : End Sub
Sub MainWnd_EditBox7_KillFocus()	: ApplyEditBox() : End Sub
Sub MainWnd_EditBox8_KillFocus()	: ApplyEditBox() : End Sub
Sub MainWnd_EditBox9_KillFocus()	: ApplyEditBox() : End Sub

Function GetOpenDeviceIndex() As Long
	GetOpenDeviceIndex=SendMessage(MWnd(ComboBox1),CB_GETCURSEL,0,0)
End Function

Sub DBM(text AS BytePtr)
	SetWindowText(hMainWnd,text)
	OutputDebugString(text)
	OutputDebugString(ex"\r\n")
End Sub


Sub MainWnd_CommandButton1_Click()
	Dim ft AS *FT232H
	Dim clk AS *FT232H_Si5351A
	Dim lcd AS *FT232H_ST7032

	ft=new FT232H()
	ft->iic->init()

	lcd=new FT232H_ST7032(ft)
	clk=new FT232H_Si5351A(ft)

	if ft->openDevice(GetOpenDeviceIndex())=FALSE Then 
		MessageBox(hMainWnd,"デバイスを開けませんでした",0,MB_ICONERROR)
		ExitSub
	End If

	Dim conf AS Si5351A_CONFIG
	GetClkgenConf(VarPtr(conf))
	clk->SetClk0(conf)

	Dim fout AS Double,sim AS String
	fout=CalcFout()

	lcd->init()
	lcd->print(0,0,"Freq Gen")
	if fout > 1 Then
		sim="MHz"
	Else
		fout=fout*1000
		sim="KHz"
	End If
	lcd->print(0,1,doubleToFormatStr("%5.01f",fout)+sim)

	delete lcd
	delete clk
	delete ft
End Sub

Sub MainWnd_EditBox7_Change()

End Sub

Function Bin$(Num As DWord)(Wide As Byte) AS String
	Dim i AS Long,buf[32] AS Byte
	if Wide>4 Then Wide=4
	if Wide=0 Then Wide=4

	For i=0 To Wide*8-1
		buf[Wide*8-1-i]=&H30 + ((Num >> i) And 1)
	Next i
	Bin$=MakeStr(buf)
End Function

Sub MainWnd_Static15_Click()
	ListupDevices()
End Sub


Sub MainWnd_CommandButton2_Click()
	Dim clk AS FT232H_Si5351A(0)
	Dim conf AS Si5351A_CONFIG

	clk.CalcParameter(GetDlgItemDouble(MWnd(EditBox6))/1000,conf)
	SetClkgenConf(VarPtr(conf))
End Sub
